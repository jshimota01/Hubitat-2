/**
 *  Alarm Handler v2.4
 *
 *  Copyright 2015 Michael Ritchie
 *
 *  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 *  in compliance with the License. You may obtain a copy of the License at:
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software distributed under the License is distributed
 *  on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License
 *  for the specific language governing permissions and limitations under the License.
 *
 */
 
definition(
    name: "Alarm Handler v2.4",
    namespace: "",
    author: "Michael Ritchie",
    description: "Create virtual devices for each alarm zone, Sends alerts from the alarm panel, synchronizes alarm panel with Smart Home Monitor",
    category: "Convenience",
    iconUrl: "http://cdn.device-icons.smartthings.com/Home/home3-icn.png",
    iconX2Url: "http://cdn.device-icons.smartthings.com/Home/home3-icn@2x.png",
    iconX3Url: "http://cdn.device-icons.smartthings.com/Home/home3-icn@3x.png")

preferences {
	page(name: "controllerSetup")
    page(name: "zoneSetup")
    page(name: "alertSetup")
}

def controllerSetup() {
    dynamicPage(name: "controllerSetup", title: "Alarm Controller and Devices", nextPage:"zoneSetup", uninstall: true) {
		section("") {
			input "arduino", "capability.alarm", title: "Alarm Controller", required: true, multiple: false
			input "howMany", "number", title: "How many alarm zones do you want to setup devices for?", required: true
		}
        section("Virtual Device Name Prefix (optional.  Example 'Alarm' for Alarm Back Door)"){
            input "namePrefix", "text", title: "Name Prefix", required: false
        }
        section("Other Preferences"){
            input("isDebugEnabled", "bool", title: "Enable debug logging?", defaultValue: false, required: false)
            href "displaySmartThingsUrlPage", title: "Display SmartThings Dashboard Url", description: "Displays the url in the Live Logging section of the IDE."
        }
	}
}

def zoneSetup() {
   	dynamicPage(name: "zoneSetup", title: "Zone Setup", nextPage:"alertSetup", install:false) {
    	for (int i=1;i<=settings.howMany;i++) {
        	section("Virtual Device " + i) {
                input "zone" + i, "number", title: "Zone Number", required: true
                input "name" + i, "string", title: "Zone Name", required: true
                input "typezone" + i, "enum", title: "Zone Type", options:["Simulated Contact Sensor","Simulated Motion Sensor","Simulated Smoke Alarm"], required: true
            }
        }    
    }
}

def alertSetup() {
    dynamicPage(name: "alertSetup", title: "Additional Features", install:true) {
        section("Integrate your alarm panel with SmartThings Smart Home Monitor.  By enabling this feature SHM will be in sync with your alarm panel.  You can arm and disarm your panel via SHM, in the Alarm Device Handler, or directly on your alarm system keypad with everything in sync."){
            input "syncSHM", "bool", title: "Sync with SHM?", defaultValue: false
        }
		section("Receive alarm alert messages via a push notification and/or an SMS message.  By enabling notifications, you will receive notifications for alarm sounding, panel power status changes (on battery, low battery, on AC), and when you try to arm your alarm and it is not in a ready state."){
            input "sendNotification", "bool", title: "Send notifications?", defaultValue: true
            input "phone", "phone", title: "Phone Number (for SMS, optional)", required: false
            input "pushAndPhone", "enum", title: "Both Push and SMS?", required: false, metadata: [values: ["Yes","No"]]
        }
        section("Additional notifications when the state changes on a contact or motion sensor.  This can include virtual devices created by this SmartApp."){
            input "contacts", "capability.contactSensor", title: "Contact Sensors", required: false, multiple: true
            input "motions", "capability.motionSensor", title: "Motion Sensors", required: false, multiple: true
        }
    }    
}

def installed() {
	initialize()
}

def updated() {
	unsubscribe()
	initialize()
}

def uninstalled() {
	removeChildDevices(getChildDevices())
}

def initialize() {
    // Subscribe to the Arduino and other contacts and motions for notifications
    if (sendNotification) {
    	subscribe(arduino, "alertMsg", alarmMsgHandler)
        if (motions) {
            subscribe(motions, "motion.active", notificationHandler)
        }
        if (contacts) {
            subscribe(contacts, "contact", notificationHandler)
        }
    }
	
	if (syncSHM) {
    	subscribe(arduino, "system", alarmHandler)
        subscribe(location, "alarmSystemStatus", shmHandler)
        
        // Declare/Clear variables to prevent duplicate updates
    	state.previousAlarmValue = arduino.currentValue("system")
        state.previousSHMValue = location.currentState("alarmSystemStatus")?.value
    }

    for (int i = 1 ; i <= howMany; i++) {        
        def zoneNumber = "zone" + settings["zone$i"].toString()
        def zoneName = settings["name$i"]
        if (namePrefix) {
        	zoneName = namePrefix + " " + zoneName
        }
        def zoneType = settings["typezone$i"]
        def deviceID = "Alarm_" + zoneNumber
        //log.debug "checking device: zoneNumber: ${zoneNumber}, name: ${zoneName}, type: ${zoneType}, deviceID: ${deviceID}"
    	
        def myDevice = getChildDevice(deviceID)
        if (!myDevice) {
            //log.debug("creating device: ${zoneName} deviceID: ${deviceID}")
            def childDevice = addChildDevice("smartthings", zoneType, deviceID, null, [name: "${zoneName}", label: "${zoneName}"])
            //log.debug("created device: ${deviceID}")
        } else {        
            myDevice.name = zoneName
            myDevice.label = zoneName
        }

        if (zoneType == "Simulated Contact Sensor") {
        	//log.debug("Subscribing to ${arduino}, ${zoneNumber}, contactHandler")
            subscribe(arduino, zoneNumber, virtualDeviceHandler)
        } else if (zoneType == "Simulated Motion Sensor") {
        	//log.debug("Subscribing to ${arduino}, ${zoneNumber}, motionHandler")
            subscribe(arduino, zoneNumber, virtualDeviceHandler)
        } else if (zoneType == "Simulated Smoke Alarm") {
        	//log.debug("Subscribing to ${arduino}, ${zoneNumber}, motionHandler")
            subscribe(arduino, zoneNumber, virtualDeviceHandler)
        }
    }
}

def virtualDeviceHandler(evt) {
    def deviceName = evt.name
    def deviceValue = evt.value
    def deviceID = "Alarm_" + deviceName
    logDebug "deviceName: ${deviceName}, deviceValue: ${deviceValue}, deviceID: '${deviceID}'"
    
    def virtualDevice = getChildDevice(deviceID)
    if (virtualDevice) {
        createChildEvent(virtualDevice, deviceValue)
    } else {
        log.debug "Could not find child device '${deviceID}'"
    }
}

//Handle alertMsg's from the AD2SmartThings device handler.  Message is set in the DH and this function just sends it.
def alarmMsgHandler(evt) {
    def msg = evt.value
	sendAlarmMsg(msg)
}

// Handle notifications for contacts and motions subscribed via the SmartApp preferences
def notificationHandler(evt) {
    def deviceName = evt.displayName
    def deviceValue = evt.value
    def msg = deviceName + " is " + deviceValue

	if (!phone || pushAndPhone != "No") {
		sendPush(msg)
	}
	if (phone) {
		sendSms(phone, msg)
	}
}

// Handle syncing the alarm panel with Smart Home Monitor
def alarmHandler(evt) {
    def deviceName = evt.displayName
    def deviceValue = evt.value
    
	if (deviceValue != "" && deviceValue != null && state.previousAlarmValue != deviceValue) {
        def translatedState = translateState(deviceValue)
        def shmState = location.currentState("alarmSystemStatus")?.value
        //log.debug "alarmHandler - deviceValue: ${deviceValue}, previousAlarmValue: ${state.previousAlarmValue}, translatedState: ${translatedState}, shmState: ${shmState}"

        if (translatedState != shmState) {
            // Set previous state variables to the new values to prevent loop.  When the sendLocationEvent fires this app will launch again.
            state.previousAlarmValue = deviceValue
            state.previousSHMValue = translatedState
            sendLocationEvent(name: "alarmSystemStatus", value: translatedState)
        }
	}
}

// Handle syncing Smart Home Monitor with the alarm panel
def shmHandler(evt) {
    def deviceName = evt.displayName
    def deviceValue = evt.value

	if (deviceValue != "" && deviceValue != null && state.previousSHMValue != deviceValue) {
		def translatedState = translateState(deviceValue)
        def alarmState = arduino.currentState("system").value.toLowerCase()
        //log.debug "shmHandler - deviceValue: ${deviceValue}, previousSHMValue: ${state.previousSHMValue}, translatedState: ${translatedState}, alarmState: ${alarmState}"
        
        if (translatedState != alarmState) {
            // Set previous state variables to the new values to prevent loop.  When the Arduino command fires this app will launch again.			
            state.previousSHMValue = deviceValue
            state.previousAlarmValue = translatedState
            switch (deviceValue) {
                case "off":
                    arduino.disarm()
                    break
                case "stay":
                    arduino.armStay()
                    break
                case "away":
                    arduino.armAway()
                    break
            }
		}
	}
}

private translateState(currentState) {
	switch (currentState) {
		case "disarmed":
			return "off"
		case "armingStay":
			return "stay"
        case "armedStay":
			return "stay"
		case "armingAway":
			return "away"
        case "armedAway":
			return "away"
        case "off":
			return "disarmed"
		case "stay":
			return "armedStay"
		case "away":
			return "armedAway"
	}
}

private createChildEvent(virtualDevice, deviceValue) {
    def virtualDeviceType = virtualDevice.typeName
    log.debug "virtualDeviceType: ${virtualDeviceType}, deviceValue: ${deviceValue}"
    
    switch (virtualDeviceType) {
        case "Simulated Contact Sensor":
            virtualDevice.sendEvent(name: "contact", value: deviceValue == "active" ? "open" : "closed")
            break
        case "Simulated Motion Sensor":
            virtualDevice.sendEvent(name: "motion", value: deviceValue)
            break
        case "Simulated Smoke Alarm":
            virtualDevice.sendEvent(name: "smoke", value: deviceValue == "active" ? "smoke" : "clear")
            break
    }
}

private sendAlarmMsg(msg) {
	if (!phone || pushAndPhone != "No") {
		//log.debug "sending push: ${msg}"
		sendPush(msg)
	}
	if (phone) {
		//log.debug "sending SMS, phone: ${phone}, msg: ${msg}"
		sendSms(phone, msg)
	}
}

private removeChildDevices(delete) {
	delete.each {deleteChildDevice(it.deviceNetworkId)}
}

private logDebug(msg) {
	if (isDebugEnabled) {
		log.debug "$msg"
	}
}

/********************************************
*    Hubitat Integration
********************************************/

mappings {
	path("/event/:deviceId/:value") {action: [GET: "api_event"]}
}

private displaySmartThingsUrlPage() {
	dynamicPage(name: "displaySmartThingsUrlPage", title: "") {
		section() {
			paragraph "The SmartThings Dashboard Url has been displayed in Live Logging"
			displaySmartThingsUrl()
		}
	}
}

private initializeAppEndpoint() {	
    try {
        def accessToken = createAccessToken()
        if (accessToken) {
            state.endpoint = apiServerUrl("/api/token/${accessToken}/smartapps/installations/${app.id}")				
        }
    } 
    catch(e) {
        log.error "$e"
        state.endpoint = null
    }
    
	return state.endpoint
}

private displaySmartThingsUrl() {
	def apiURL = initializeAppEndpoint()
    log.info "SmartThings API Url: ${apiURL}"	
}

private api_event() {
	def deviceId = params?.deviceId
    def deviceValue = params?.value
    logDebug "deviceId: ${deviceId}, deviceValue: ${deviceValue}"
    
    if (deviceId == "alertMsg") {
    	sendAlarmMsg(deviceValue)
    } else {
    	deviceId = "Alarm_" + deviceId
        def child = getChildDevice(deviceId)
        if (child) {
            createChildEvent(child, deviceValue)
        }
        else {
            logDebug "Device ${deviceId} not found"
        }
    }

	return [""]
}
